plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
}

def release_store_password
def release_key_password
def release_key_alias
def pin_alias
def db_pass_alias

def keystoreProperties = new Properties()
if (rootProject.file("keystore.properties").exists()) {
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    release_store_password = keystoreProperties['RELEASE_STORE_PASSWORD']
    release_key_password = keystoreProperties['RELEASE_KEY_PASSWORD']
    release_key_alias = keystoreProperties['RELEASE_KEY_ALIAS']
    pin_alias = keystoreProperties['PIN_ALIAS']
    db_pass_alias = keystoreProperties['DB_PASS_ALIAS']
} else {
    release_store_password = System.env.RELEASE_STORE_PASSWORD
    release_key_password = System.env.RELEASE_KEY_PASSWORD
    release_key_alias = System.env.RELEASE_KEY_ALIAS
    pin_alias = System.env.PIN_ALIAS
    db_pass_alias = System.env.DB_PASS_ALIAS
}

def versionPropsFile = rootProject.file('version.properties')
Properties versionProps = new Properties()
versionProps.load(new FileInputStream(versionPropsFile))
def verCode = versionProps['VERSION_CODE'].toInteger()

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.guralnya.notification_tracker"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode verCode
        versionName "1.1.$verCode"
//        setProperty("archivesBaseName", "notification_tracker-v" + verCode)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndkVersion '21.2.6472646'
    }
    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file("../NotificationTracker.jks")
            storePassword = release_store_password
            keyAlias = release_key_alias
            keyPassword = release_key_password
        }
        debug {
            storeFile file('../debug.keystore')
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
            signingConfig signingConfigs.release
            buildConfigField "String", "PIN_ALIAS", "\"$pin_alias\""
            buildConfigField "String", "DB_PASS_ALIAS", "\"$db_pass_alias\""
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", "PIN_ALIAS", "\"1234\""
            buildConfigField "String", "DB_PASS_ALIAS", "\"1234\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'com.guralnya.notification_tracker'
}

def static getTime() {
    return new Date().format("ddMMHHmm", TimeZone.getDefault())
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation 'com.google.devtools.ksp:symbol-processing-api:1.9.10-1.0.13'
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //Room
    annotationProcessor "androidx.room:room-compiler:2.5.2"
    ksp "androidx.room:room-compiler:2.5.2"
    implementation "androidx.room:room-runtime:2.5.2"
    implementation "androidx.room:room-ktx:2.5.2"
//    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //Cipher for DB
    implementation 'net.zetetic:android-database-sqlcipher:4.3.0'
    implementation "androidx.sqlite:sqlite-ktx:2.3.1"

    //koin
    implementation "io.insert-koin:koin-android:3.5.0"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"

    //Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.4"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.4"

    //Joda Time
    implementation 'net.danlew:android.joda:2.10.1.2'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
